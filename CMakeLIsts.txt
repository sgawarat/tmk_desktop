cmake_minimum_required(VERSION 3.18)
project(tmk_desktop)

set(TMK_CORE_DIR "" CACHE PATH "Path to the tmk_core directory")

if(NOT IS_ABSOLUTE ${TMK_CORE_DIR})
    set(TMK_CORE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/${TMK_CORE_DIR}")
endif()

set(TMK_DESKTOP_KEYMAP_DIR "" CACHE PATH "Path to the keymap directory")

if(NOT IS_ABSOLUTE ${TMK_DESKTOP_KEYMAP_DIR})
    set(TMK_DESKTOP_KEYMAP_DIR "${CMAKE_CURRENT_SOURCE_DIR}/${TMK_DESKTOP_KEYMAP_DIR}")
endif()

if(NOT EXISTS ${TMK_CORE_DIR})
    message(FATAL_ERROR "tmk_core directory '${TMK_CORE_DIR}' NOT FOUND")
endif()

if(NOT EXISTS ${TMK_DESKTOP_KEYMAP_DIR})
    message(FATAL_ERROR "keymap directory '${TMK_DESKTOP_KEYMAP_DIR}' NOT FOUND")
endif()

add_library(config INTERFACE)
target_precompile_headers(config INTERFACE
    ./include/tmk_desktop/stable.h
    ${TMK_DESKTOP_KEYMAP_DIR}/config.h
)

if(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
    target_compile_options(config INTERFACE
        /W3
    )
else()
    target_compile_options(config INTERFACE
        -Wall
        -Wextra
        -Wno-unused-parameter
        -Wno-c++98-compat
        -Wno-c++98-compat-pedantic
    )
endif()

target_compile_features(config INTERFACE
    cxx_std_20
)
target_compile_definitions(config INTERFACE
    ACTIONMAP_ENABLE
    NO_DEBUG
    NO_PRINT

    _CRT_SECURE_NO_WARNINGS
    WIN32_LEAN_AND_MEAN
    NOMINMAX
    UNICODE

    # アクションで定義される修飾キー入力が次のアクションに影響を与えないようにする
    TMK_DESKTOP_FIX_WEAK_MODS
)
target_include_directories(config INTERFACE
    ${TMK_CORE_DIR}
    ./include
)

add_subdirectory(src)
add_subdirectory(${TMK_DESKTOP_KEYMAP_DIR})
add_subdirectory(platforms)
add_subdirectory(tools/key_test)
